---
layout: post
title: "Universal Shift Register (HDLBits)"
subtitle: "Three State Universal Shift Register"
categories: [Verilog]
date: 2026-02-22
lang: zh-Hant
---

## ğŸ“Œ é¡Œç›®

Consider the n-bit shift register circuit shown below:

<!-- ![](/assets/26_0222/Exams_2014q4.png) -->
<p align="center">
<img src="{{ '/assets/26_0222/Exams_2014q4.png' | relative_url }}" width="600">
</p>

Write a top-level Verilog module (named top_module) for the shift register, assuming that n = 4. Instantiate four copies of your MUXDFF subcircuit in your top-level module. Assume that you are going to implement the circuit on the DE2 board.

* Connect the R inputs to the SW switches,
* clk to KEY[0],
* E to KEY[1],
* L to KEY[2], and
* w to KEY[3].
* Connect the outputs to the red lights LEDR[3:0].

(Reuse your MUXDFF from [exams/2014_q4a](https://hdlbits.01xz.net/wiki/Exams/2014_q4a).)
(My Blog Answer: [DFF and Gates](https://brandon-git-hub.github.io/TechBlog/verilog/2025/11/08/dff-and-gates.html))

### Universal Shift Register

æ­¤ Shift Register å¯è¦–ä½œä¸€å€‹ç°¡åŒ–ç‰ˆçš„ Universal Shift Register.
çµåˆäº† **ä¸¦åˆ—è¼‰å…¥ (Parallel Load)**, **å³ç§»**, **ä¿æŒè³‡æ–™ (Hold)** åŠŸèƒ½ã€‚

| æ“ä½œæ¨¡å¼ (Mode) | Load (L) | Enable (E) | ä¸‹ä¸€å€‹ç‹€æ…‹ ($Q_{next}$) | èªªæ˜ (Description) |
| --- | --- | --- | --- | --- |
| **Parallel Load** | **1** | X | `SW[i]` | **æœ€é«˜å„ªå…ˆæ¬Š**ã€‚åŒæ­¥å°‡å¤–éƒ¨ä¸¦è¡Œè¼¸å…¥è¼‰å…¥æš«å­˜å™¨ã€‚ |
| **Shift Right** | **0** | **1** | `w` (i.e., `LEDR[i+1]`) | åŸ·è¡Œç§»ä½æ“ä½œã€‚è³‡æ–™ç”± `KEY[3]` é€²å…¥ï¼Œå‘ä½ä½å…ƒç§»å‹•ã€‚ |
| **Hold** | **0** | **0** | `Q` (è‡ªèº«) | ä¿æŒç›®å‰ç‹€æ…‹ä¸è®Šï¼Œä¸é€²è¡Œè¼‰å…¥æˆ–ç§»ä½ã€‚ |

é€™ç¨®ç”± MUX èˆ‡ DFF çµ„æˆçš„çµæ§‹ï¼Œåœ¨ç©é«”é›»è·¯æ¸¬è©¦ï¼ˆDesign for Testï¼‰ä¸­å³ç‚º Scan Cell çš„åŸºæœ¬é››å½¢ã€‚

## ğŸ§‘â€ğŸ’» ç¯„ä¾‹ç¨‹å¼

### RTL Code

```verilog
module top_module #(
    parameter WIDTH = 4
) (
    input [3:0] SW,
    input [3:0] KEY,
    output [3:0] LEDR
);
    genvar i;
    generate
        for (i=0; i<WIDTH; i=i+1) begin: Exams_2014q4
            MUXDFF md(.clk(KEY[0]), .w((i == WIDTH-1 ) ? KEY[3] : LEDR[i+1]), .R(SW[i]), .E(KEY[1]), .L(KEY[2]), .Q(LEDR[i]));
        end
    endgenerate

endmodule

module MUXDFF (
    input clk,
    input w, R, E, L,
    output reg Q
);
    
    always @ (posedge clk) begin
        Q <= (L)? R : ((E)? w : Q);
    end

endmodule
```

### Testbench Code

```verilog
`timescale 1ns/1ps

module tb_top_module;

    // Inputs
    reg [3:0] SW;
    reg [3:0] KEY; // KEY[0]: clk, KEY[1]: E, KEY[2]: L, KEY[3]: w

    // Outputs
    wire [3:0] LEDR;

    // Instantiate the Unit Under Test (UUT)
    top_module dut (
        .SW(SW), 
        .KEY(KEY), 
        .LEDR(LEDR)
    );

    // Clock generation (KEY[0])
    initial begin
        KEY[0] = 0;
        forever #5 KEY[0] = ~KEY[0]; // 10ns period
    end

    initial begin
        $dumpfile("tb_top_module.vcd");
        $dumpvars(0, tb_top_module);

        // Initialize Inputs
        SW = 0;
        KEY[3:1] = 0; // Clear control signals (w, L, E)

        // Wait for global reset or startup
        #20;

        // Test Case 1: Load (L=1)
        $display("Test Case 1: Parallel Load SW = 4'b1011");
        SW = 4'b1011;
        KEY[2] = 1; // L = 1 (Load)
        KEY[1] = 0; // E = 0
        KEY[3] = 0; // w = 0 (don't care)
        #10; // Wait for clock edge
        
        if (LEDR !== 4'b1011) $display("Error: Load failed. Expected 1011, got %b", LEDR);
        else $display("Pass: Loaded %b", LEDR);

        // Test Case 2: Hold (L=0, E=0)
        $display("Test Case 2: Hold value");
        KEY[2] = 0; // L = 0
        KEY[1] = 0; // E = 0
        SW = 4'b0000; // Change SW to ensure we are not loading
        #10;
        
        if (LEDR !== 4'b1011) $display("Error: Hold failed. Expected 1011, got %b", LEDR);
        else $display("Pass: Held value %b", LEDR);

        // Test Case 3: Shift Right with w=0 (E=1)
        $display("Test Case 3: Shift Right (input 0)");
        KEY[2] = 0; // L = 0
        KEY[1] = 1; // E = 1 (Shift)
        KEY[3] = 0; // w = 0
        
        #10; // Shift 1: 0101
        if (LEDR !== 4'b0101) $display("Error: Shift 1 failed. Expected 0101, got %b", LEDR);
        else $display("Pass: Shifted to %b", LEDR);

        #10; // Shift 2: 0010
        if (LEDR !== 4'b0010) $display("Error: Shift 2 failed. Expected 0010, got %b", LEDR);
        else $display("Pass: Shifted to %b", LEDR);

        // Test Case 4: Shift Right with w=1
        $display("Test Case 4: Shift Right (input 1)");
        KEY[3] = 1; // w = 1
        
        #10; // Shift 3: 1001
        if (LEDR !== 4'b1001) $display("Error: Shift 3 failed. Expected 1001, got %b", LEDR);
        else $display("Pass: Shifted to %b", LEDR);

        #10; // Shift 4: 1100
        if (LEDR !== 4'b1100) $display("Error: Shift 4 failed. Expected 1100, got %b", LEDR);
        else $display("Pass: Shifted to %b", LEDR);

        #20;
        $finish;
    end

endmodule
```

## ğŸ”¬ å¯¦é©—çµæœ

### Simulation Waveform

* Test Case 1: Load (L=1)

    ç•¶ L=1, E=0, w=0 æ™‚ï¼ŒåŸ·è¡Œ Load dataï¼Œè€Œæ­¤æ™‚ SW é€å…¥çš„æ˜¯ ```4'b1011```ï¼Œå› æ­¤å¯ä»¥çœ‹åˆ° ```LEDR = 4'b1011```.

<!-- ![](/assets/26_0222/Simulation_test_case1.png) -->
<p align="center">
<img src="{{ '/assets/26_0222/Simulation_test_case1.png' | relative_url }}" width="600">
</p>

* Test Case 2: Hold (L=0, E=0)

    ç•¶ L=0, E=0, w=0 æ™‚ï¼ŒåŸ·è¡Œ Holdï¼Œæ‰€ä»¥å¯ä»¥çœ‹åˆ° ```LEDR``` ä»æ˜¯ ```4'b1011```.

<!-- ![](/assets/26_0222/Simulation_test_case2.png) -->
<p align="center">
<img src="{{ '/assets/26_0222/Simulation_test_case2.png' | relative_url }}" width="600">
</p>

* Test Case 3: Shift Right with w=0 (E=1)

    ç•¶ L=0, E=1, w=0 æ™‚ï¼Œå°‡ç¾æœ‰å€¼å‘å³å‚³ä¸”ä¸æœƒ Rotateï¼ŒLSB ç›´æ¥å‘å³ç›´æ¥æ¨æ£„ã€‚

<!-- ![](/assets/26_0222/Simulation_test_case3.png) -->
<p align="center">
<img src="{{ '/assets/26_0222/Simulation_test_case3.png' | relative_url }}" width="600">
</p>

* Test Case 4: Shift Right with w=1

    ç•¶ L=0, E=1, w=1 æ™‚ï¼Œå°‡ç¾æœ‰å€¼å‘å³å‚³ä¸” MSB å‚³å…¥ w=1ã€‚

<!-- ![](/assets/26_0222/Simulation_test_case4.png) -->
<p align="center">
<img src="{{ '/assets/26_0222/Simulation_test_case4.png' | relative_url }}" width="800">
</p>

### Synthesis RTL-level Schematic

<!-- ![](/assets/26_0222/schematic_rtl.svg) -->

<p align="center">
<img src="{{ '/assets/26_0222/schematic_rtl.svg' | relative_url }}" width="700">
</p>

## ğŸ“š Reference
* [HDLBits Problem - Exams/2014 q4b](https://hdlbits.01xz.net/wiki/Exams/2014_q4b)
